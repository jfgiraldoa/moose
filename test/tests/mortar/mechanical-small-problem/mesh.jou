# This script meshes a unit square which is split by a finite width
# gap at x=1/4 with a non-conforming mesh on both sides.

# cd '/Users/petejw/code/peterson_git/cubit/nodal_normals'
# play "nodal_normals_test_offset_nonmatching_gap.jou"

# Clear any existing model
reset

# Create outer part
create brick width 1 depth 1 height 1

# Create a gap of width .01 around x=1/4
webcut body all plane xplane offset {.25 - 0.005}
webcut body all plane xplane offset {.25 + 0.005}

# Remove middle body
delete body 3

# Note: In Cubit's numbering, body 2 is actually on the left!
# block 1 = left
# block 2 = right
block 1 surf 15
block 2 surf 20

# Give the blocks names for easier identification.
block 1 name 'left'
block 2 name 'right'

# Assign sidesets to the gap interfaces.
sideset 1 curve 23
sideset 1 name 'left_sideset'
sideset 2 curve 29
sideset 2 name 'right_sideset'

# We will probably want other sidesets for specifying BCs too. We
# number everything counter clockwise starting from the bottom.
sideset 3 curve 28
sideset 3 name 'bottom_left'
sideset 4 curve 36
sideset 4 name 'bottom_right'
sideset 5 curve 1
sideset 5 name 'outside_right'
sideset 6 curve 35
sideset 6 name 'top_right'
sideset 7 curve 26
sideset 7 name 'top_left'
sideset 8 curve 3
sideset 8 name 'outside_left'

# The base intervals are multiplied by the interval_multiplier variable
# to get successively-refined grids.
# {interval_multiplier = 1}

# Set the mesh interval along the quarter circle arcs. Setting no
# interval results in 14 element faces on the boundary. 20 seems to
# generate something a little better looking. Here, we use mismatched
# intervals to test the nonconforming initial mesh case. This should
# work fine when there is an infinitesimal width "slit" in the mesh...

# outside_left and left_sideset
curve 3 23 interval {3 * interval_multiplier}
# outside_right and right_sideset
curve 1 29 interval {2 * interval_multiplier}
# bottom_left and top_left
curve 26 28 interval {1 * interval_multiplier}
# bottom_right and top_right
curve 35 36 interval {1 * interval_multiplier}

# Mesh the part on the left with triangles.
surf 15 scheme Map
mesh surf 15

# Mesh the part on the right with quads.
surf 20 scheme Map
mesh surf 20

# Set the geometric element type to use in each block.
block 1 element type QUAD4
block 2 element type QUAD4

# Write to file.
export mesh "mesh.e" dimension 2 block 1 2 overwrite

# Local Variables:
# mode: shell-script
# End:
